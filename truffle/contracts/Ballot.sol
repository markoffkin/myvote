pragma solidity ^0.4.11;

/// @title Voting. Modified from Ethereum docs.
contract Ballot {

    // This declares a new complex type which will
    // be used for variables later.
    // It will represent a single voter.
    struct Voter {
        bool voted;   // if true, that person already voted
        bool canVote; // if true, that person can vote
        uint vote;    // index of the voted proposal
    }

    // This is a type for a single proposal.
    struct Proposal {
        bytes32 name;   // short name (up to 32 bytes)
        uint voteCount; // number of accumulated votes
    }

    address public government;
    string proposalDescription;

    // This declares a state variable that
    // stores a `Voter` struct for each possible address.
    mapping(address => Voter) public voters;

    // A dynamically-sized array of `Proposal` structs.
    Proposal[] public proposals;

    /// Create a new ballot to choose one of `proposalNames` and 'proposalDescriptions'.
    function Ballot(string description, bytes32[] proposalNames) {
        government = msg.sender;
        proposalDescription = description;
        voters[government].canVote = false;

        // For each of the provided proposal names,
        // create a new proposal object and add it
        // to the end of the array.
        for (uint i = 0; i < proposalNames.length; i++) {
            // `Proposal({...})` creates a temporary
            // Proposal object and `proposals.push(...)`
            // appends it to the end of `proposals`.
            proposals.push(Proposal({
                name: proposalNames[i],
                voteCount: 0
            }));
        }
    }

    // Set the 'voter 'the right to vote on this ballot.
    // May only be called by `government`.
    function setRightToVote(address voter, bool canVote) {
        // If the argument of `require` evaluates to `false`,
        // it terminates and reverts all changes to
        // the state and to Ether balances. It is often
        // a good idea to use this if functions are
        // called incorrectly. But watch out, this
        // will currently also consume all provided gas
        // (this is planned to change in the future).
        require((msg.sender == government) && !voters[voter].voted && (voters[voter].canVote != canVote));
        voters[voter].canVote = canVote;
    }

    /// Give your vote to `proposals[proposal]`.
    function vote(uint proposal) {
        Voter storage sender = voters[msg.sender];
        require(!sender.voted);
        require(!sender.canVote);
        sender.voted = true;
        sender.vote = proposal;
        sender.canVote = false;

        // If `proposal` is out of the range of the array,
        // this will throw automatically and revert all
        // changes.
        proposals[proposal].voteCount += 1;
    }

    /// @dev Computes the winning proposal taking all
    /// previous votes into account.
    function getWinningProposal() constant
            returns (uint winningProposal)
    {
        uint winningVoteCount = 0;
        for (uint p = 0; p < proposals.length; p++) {
            if (proposals[p].voteCount > winningVoteCount) {
                winningVoteCount = proposals[p].voteCount;
                winningProposal = p;
            }
        }
    }

    // Calls winningProposal() function to get the index
    // of the winner contained in the proposals array and then
    // returns the name of the winner
    function getWinnerName() constant
            returns (bytes32 winnerName)
    {
        winnerName = proposals[getWinningProposal()].name;
    }


    function getDescription() constant returns (string desc)
    {
        desc = proposalDescription;
    }

    function getProposalName(uint8 proposalNumber) constant returns (bytes32 propName)
    {
        propName = proposals[proposalNumber].name;
    }

    function getProposalVoteCount(uint8 proposalNumber) constant returns (uint cnt)
    {
        cnt = proposals[proposalNumber].voteCount;
    }

    function canMeVote() constant returns (bool canVote)
    {
        Voter storage sender = voters[msg.sender];
        canVote = sender.canVote;
    }

    function hasMeVoted() constant returns (bool hasVoted)
    {
        Voter storage sender = voters[msg.sender];
        hasVoted = sender.voted;
    }
}

